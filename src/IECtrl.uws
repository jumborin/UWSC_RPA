//IEを操作するクラス
CLASS IECtrl

	//変数を定義
	DIM ie
	DIM ウインドウID
	
	
	// 指定URLをIEで開く。
	PROCEDURE start(URL)
		
		// IEを起動する
		ie = createOLEobj("InternetExplorer.Application")
		
		// IEを表示する。
		ie.visible = True
		
		// ウインドウIDを取得
		ウインドウID = HNDtoID(ie.hwnd)
		
		// IEを最大化する
		CtrlWin(ウインドウID,MAX)
		
		// 指定URLに遷移する。
		ie.navigate(URL)
		
		// しばし待機する
		ieWait(ie)
	FEND
	
	
	// 指定URLに遷移する。
	PROCEDURE move(ウインドウタイトル, URL)
		
		// ウインドウタイトルから対象のIEのウインドウを取得する。
		ie = GETACTIVEOLEOBJ( "InternetExplorer.Application" , ウインドウタイトル )
		
		// 指定URLに遷移する。
		ie.navigate( URL )
		
		// しばし待機する
		ieWait( ie )
	FEND
	
	
	// 指定IDの入力欄に指定文字列を入力する。
	PROCEDURE input(ウインドウタイトル, 指定ID, 指定文字列)
		
		// ウインドウタイトルから対象のIEのウインドウを取得する。
		ie = GETACTIVEOLEOBJ("InternetExplorer.Application",ウインドウタイトル)
		
		// 指定IDの入力欄に指定文字列を入力する。
		ie.Document.getElementById(指定ID).Value = 指定文字列
		
		// しばし待機する。
		ieWait(ie)
	FEND
	
	
	// 指定IDのボタンをクリックする。
	PROCEDURE click(ウインドウタイトル, リンク名)
		
		// ウインドウタイトルから対象のIEのウインドウを取得する。
		ie = GETACTIVEOLEOBJ( "InternetExplorer.Application" , ウインドウタイトル)
		
		// 指定IDのリンクをクリックする。
		IELINK(ie,リンク名,1,true)
		// しばし待機する。
		ieWait( ie )
	FEND
	
	//IEが処理中の間、待機する。
	PROCEDURE ieWait(ie)
		repeat
			sleep(0.5)
		until (! ie.busy) and (ie.readystate = 4)
		sleep(1)
	FEND
	
ENDCLASS